AC_PREREQ(2.50)
AC_INIT([libzmapfoocanvas], [0.1])
AC_CONFIG_SRCDIR([libfoocanvas/foo-canvas.c])

AC_CONFIG_HEADERS([config.h])
dnl [dist-bz2] removed from the AM_INIT_AUTOMAKE()
AM_INIT_AUTOMAKE([])

AM_MAINTAINER_MODE

GNOME_COMMON_INIT

AC_ISC_POSIX
AC_PROG_CC
AC_HEADER_STDC


dnl Disable shared libs so foocanvas is bound in to zmap.
AC_DISABLE_SHARED
AM_PROG_LIBTOOL

GNOME_COMPILE_WARNINGS

GETTEXT_PACKAGE=libfoocanvas-2.0
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE",
	[The package name, for gettext])

ALL_LINGUAS=""
AM_GLIB_GNU_GETTEXT

GNOME_COMPILE_WARNINGS(maximum)

dnl
dnl GTK+ check
dnl
CANVAS_MODULES="gtk+-2.0 >= 1.3.12 pango >= 0.23"
PKG_CHECK_MODULES(LIBFOOCANVAS, $CANVAS_MODULES)
AC_SUBST(LIBFOOCANVAS_CFLAGS)
AC_SUBST(LIBFOOCANVAS_LIBS)

dnl Checks for Xft/XRender
dnl
AC_CHECK_LIB(Xrender, XRenderFindFormat, 
	[AC_SUBST(RENDER_LIBS, "-lXrender -lXext")
	 AC_DEFINE(HAVE_RENDER, 1, [Define if libXrender is available.])],
	[AC_SUBST(RENDER_LIBS, "")],
	[-lXext])

AC_ARG_VAR(GLIB_GENMARSHAL, [The glib-genmarshal executable.])
AC_CHECK_PROG(GLIB_GENMARSHAL, glib-genmarshal, glib-genmarshal)

AC_ARG_WITH(html-dir, [  --with-html-dir=PATH path to installed docs ])

if test "x$with_html_dir" = "x" ; then
  AC_SUBST(HTML_DIR, '${datadir}/gtk-doc/html')
else
  AC_SUBST(HTML_DIR, $with_html_dir)
fi

dnl MAC OS X doesn't like this... (requires gtk-doc, which requires a million other packages)
dnl GTK_DOC_CHECK([1.0])

AC_CONFIG_FILES([
Makefile
libfoocanvas/Makefile
libfoocanvas/libzmapfoocanvas.pc
demos/Makefile
])
AC_OUTPUT
