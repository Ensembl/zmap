#!/usr/bin/env perl

# copied from Otterlace script bam_get_align and ammedned by mh17 to be otterlace free
# you do however have to provide Bio::DB::Sam, i'm using the otterlace build for testing

use strict;
use warnings;

use Carp;
use Getopt::Long 'GetOptions';
use Pod::Usage;

use URI::Escape qw(uri_unescape);

use Bio::DB::Sam;
use Bio::Server::BAM;


=begin comment

# (move this into POD at the end)

for Sam/BAM we need:

file name/ URL

sequence name/id (eg chromosome, but can need a prefix, depends what's in the file
start
end
type   => 'match'

for ZMap we need:
GFF version
source name for output

mapping information: assembly to reference: file or DB??

=cut


# parameters
my %params = ();
my @options = qw ( help man gff_version=i chr_prefix=s file=s chr=s start=i end=i source=s sequence=s);
my @required = qw ( file chr start end source );

my $attributes_format;
my $strands = { 1  => '+', -1 => '-' };
my $source;
my $sequence;


sub get_params
{
 	# defaults
	$params {'gff_version'}= 2;		# default for now as ZMap doesn't do v3 yet
	$params {'chr_prefix'} = '';

	GetOptions(\%params, @options ) or pod2usage(2);

	pod2usage(1) if defined $params { 'help' };
	pod2usage(-exitstatus => 0, -verbose => 2) if defined $params {'man'} ;

	my $missing = 0;
	foreach my $param (@required)
	{
		if (!defined $params { $param })
		{
			print "$param not specified\n";
			$missing++;
		}
	}
	pod2usage(1) if $missing;

	$params{$_} = uri_unescape($params{$_}) for keys %params;

	if(defined $params { 'sequence' })
	{
		$sequence = $params { 'sequence' };
	}
	else
	{
		$sequence = $params {'chr_prefix'} . $params {'chr'};
	}


	$attributes_format = $params { 'gff_version' } == 3 ? '%s=%s' : '%s %s';
}


sub print_header
{
    printf "##gff-version %d\n##sequence-region %s %d %d\n", $params { 'gff_version' }, $sequence, $params { 'start' }, $params { 'end' } ;
    return;
}


sub gff_escape {
    # escapes a minimal set of characters
    s/([\t\r\n[:cntrl:];=%&])/sprintf "%%%02X", ord($1)/eg;
    return;
}

sub gff_escape_seqid {
    # escapes everything except a restricted set of characters
    s/([^-a-zA-Z0-9.:^*$@!+_?|])/sprintf "%%%02X", ord($1)/eg;
    return;
}

sub gff_escape_source {
    # escapes everything except a restricted set of characters
    s/([^-a-zA-Z0-9.:^*$@!+_? ])/sprintf "%%%02X", ord($1)/eg;
    return;
}

sub gff_escape_attribute {
    # like gff_escape, but escapes commas too
    s/([\t\r\n[:cntrl:];=%&,])/sprintf "%%%02X", ord($1)/eg;
    return;
}

sub gff_escape_target {
    # like gff_escape, but escapes commas and spaces too
    s/([\t\r\n[:cntrl:];=%&, ])/sprintf "%%%02X", ord($1)/eg;
    return;
}


sub print_feature {
    my ($feature, $start, $end) = @_;

    my $name = $feature->name;
    gff_escape_target for $name;

    # the -gff_feature_source flag overrides the source from the BAM file
	# No: the BAM source is usually a bit useless
    # my $source = $params { 'source' } ; # || $feature->source_tag;

    my ($score) = $feature->get_tag_values('AS');

    my @fields =
        (
         'similarity',
         (sprintf "%d", $start),
         (sprintf "%d", $end),
         (defined $score ? (sprintf "%f", $score) : '.'),
         $strands->{$feature->strand},
         $feature->phase || '.',
        );

    # $feature->cigar_str has the wrong format for the Gap tag
    # (eg. 76M instead of M76) so we have to build it from
    # $feature->cigar_array (which, strangely, has the operator
    # and count arguments in the right order for the Gap tag even
    # though $feature->cigar_str has them in the wrong order)

    # the Target tag - escaping this requires special handling of
    # spaces in the query name

    my $query = $feature->query;

    my @align_fields = ($query->start, $query->end);
    my $query_strand = $strands->{$query->strand};
    push @align_fields, $query_strand if $query_strand;

    my $q_length = $feature->l_qseq;

    my @attributes = (
        Class       => qq{"Sequence"},
        Name        => qq{"$name"},
        Align       => join(' ', @align_fields),
        Length      => $feature->l_qseq,
        percentID   => 100,
        sequence    => $query->dna,
        );

    my $cigar = $feature->cigar_str;
    $cigar =~ s/\d+S//g;    # Remove BAM "S" clipping strings from CIGAR
    if ($cigar !~ /^\d+M$/) {
        # It isn't a simple match string, so we do need CIGAR
        push(@attributes, cigar_bam => qq{"$cigar"});
    }

    my @attribute_strings = ();
    while ( my ($key, $value) = splice(@attributes, 0, 2) ) {
        push @attribute_strings, sprintf $attributes_format, $key, $value;
    }
    my $attributes_string = join " ; ", @attribute_strings;

    gff_escape_source for $source;
    gff_escape for @fields;
    print join("\t", $sequence, $source, @fields, $attributes_string), "\n";

    return;
}



=begin comment

# get the mapping from the Otter server
my $client = Bio::Otter::Lace::Defaults::make_Client();
my $mapping_xml = $client->otter_response_content(
    'GET', 'get_mapping', {
        dataset => $dataset,
        cs      => $csver,
        chr     => $chr,
        start   => $start,
        end     => $end,
    });
my $mapping = Bio::Otter::Mapping->new_from_xml($mapping_xml);

# map and print the features
_print_header;
$mapping->do_features($bam_fetch, $start, $end, \&_print_feature);

=cut


sub main
{
	get_params();

	my $chr = $params { 'chr_prefix' } . $params { 'chr' } ;
	gff_escape_seqid for $sequence;

	$source = $params { 'source' };
	gff_escape_source for $source;

	my $sam = Bio::DB::Sam->new
	(
		-bam   => uri_unescape($params { 'file' }),
		# -fasta => $fasta_path,
	);

	my $bam_fetch = Bio::Server::BAM->new
	(
		-sam        => $sam
	);


	# map first then request.... move this to inside the mapping function
	my $features = $bam_fetch->features ( $chr, $params { 'start' },  $params { 'end' } );

# if it's a ref
#	printf "found %d matches\n", scalar @{$$features}  ;
	printf STDERR "found %d matches\n", scalar @{$features}  ;

	print_header();

	foreach my $feat (@{$features})
	{
		print_feature($feat, $feat->start, $feat->end );
	}
}

main;

__END__

=head1 NAME

zmap_get_bam request data and output as GFF

=head1 SYNOPSIS

zmap_get_bam [--help] [--man] --file=[http:|ftp:]/path/to/file --start=123 --end=456 --source=name --chr=IV [--chr_prefix=CHROMOSOME_] [--gff_version=3]

=head1 OPTIONS

=over 8
=item B<-help>
Print a brief help message and exits.
=item B<-man>
Prints the manual page and exits.
=back

=head1 DESCRIPTION

B<This program> will read the given input file(s) and do someting
useful with the contents thereof.
=cut


